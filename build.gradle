buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://plugins.gradle.org/m2/' }
        maven { url = 'https://maven.minecraftplus.org/'}
        maven { url = 'https://maven.minecraftforge.net/'}
    }
    dependencies {
        classpath 'org.minecraftplus:srgutils:0.4.29'
    }
}

plugins {
    id 'java'
    id 'de.undercouch.download'
    id 'uk.jamierocks.propatcher' version '2.0.1' apply false
    id 'org.barfuin.gradle.taskinfo' version '1.0.5'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
}

group = "org.minecraftplus"
version = "0.0.1-SNAPSHOT"



import org.minecraftplus.gradle.*
import net.minecraftforge.mcpconfig.tasks.*
import uk.jamierocks.propatcher.task.*

net.minecraftforge.mcpconfig.tasks.Utils.init()

project.ext {
    PATH_BUILD = file('build').absolutePath + '/'
    PATH_PROJECT = file('version').absolutePath + '/'
    PATH_LIBRARIES = PATH_BUILD + 'libraries/'

    PATH_SOURCE = file('src').absolutePath + '/'
    PATH_SOURCE_PATCHES = PATH_SOURCE + 'patches/'

    PATH_PROJECTS = file('projects').absolutePath + '/'
    PATH_PROJECTS_ASSETS = PATH_PROJECTS + 'assets'
    PATH_PROJECTS_NATIVES = PATH_PROJECTS + 'natives'

    CONFIG = file(PATH_PROJECT + 'config.json').json

    MINECRAFT_VERSION = CONFIG.get('minecraft_version', '1.15.2')
    JAVA_TARGET = CONFIG.get('java_target', 8)

    FERNFLOWER = Utils.readConfig(CONFIG, 'fernflower', [
            version: 'net.minecraftforge:forgeflower:1.5.498.22',
            args: ["-din=1", "-rbr=1", "-dgs=1", "-asc=1", "-rsy=1", "-iec=1", "-isl=0", "-log=TRACE", "-cfg", "{libraries}", "{input}", "{output}"],
            jvmargs: ["-Xmx4G"]
    ])
    MERGETOOL = Utils.readConfig(CONFIG, 'merge', [
            version: 'net.minecraftforge:mergetool:1.1.3:fatjar',
            args: ["--client", "{client}", "--server", "{server}", "--ann", "{version}", "--output", "{output}"]
    ])
    RENAMETOOL = Utils.readConfig(CONFIG, 'rename', [
            version: 'org.minecraftplus:vignette:0.3.0.0',
            args: ["--jar-in", "{input}", "--jar-out", "{output}", "--mapping-format", "tsrg2", "--mappings", "{mappings}", "--cfg", "{libraries}", "--deduce-param-names", "--dictionary", "{dictionary}", "--create-inits", "--fernflower-meta"]
    ])
}

java.toolchain {
    languageVersion = JavaLanguageVersion.of(JAVA_TARGET)
}



task downloadMergeTool(group: 'mcplus download', type: DownloadTool) {
    description = 'Downloads merge tool'

    config MERGETOOL, PATH_LIBRARIES
}
task downloadRenameTool(group: 'mcplus download', type: DownloadTool) {
    description = 'Downloads rename tool'

    config RENAMETOOL, PATH_LIBRARIES
}
task downloadFernflower(group: 'mcplus download', type: DownloadTool) {
    description = 'Downloads decompiler'

    config FERNFLOWER, PATH_LIBRARIES
}

task downloadManifest(type: Download) {
    group = 'mcplus download'
    description = 'Downloads version manifest from mojang.com.'

    src 'https://launchermeta.mojang.com/mc/game/version_manifest_v2.json'
    dest file(PATH_BUILD + 'version/manifest.json')
    useETag 'all'
    onlyIfModified true
    quiet true
}

task downloadVersionJson(type: Download) {
    group = 'mcplus download'
    description = 'Downloads minecraft version JSON configuration.'
    dependsOn rootProject.downloadManifest

    inputs.file rootProject.downloadManifest.dest
    src { downloadManifest.dest.json.versions.find{ it.id == MINECRAFT_VERSION }.url }
    dest file(PATH_BUILD + 'version/version.json')
    useETag 'all'
    onlyIfModified true
    quiet true
}

/**
 * Download client and server JARs
 */
task downloadVersionJars(group: 'mcplus download') {
    description = 'Downloads minecraft version jars (client and server).'
}

['client', 'server'].each { id ->
    def taskName = 'downloadVersion' + id.capitalize() + "Jar"
    def versionTask = task "${taskName}" (type: Download) {
        group = 'mcplus download ' + id
        dependsOn downloadVersionJson

        inputs.file downloadVersionJson.dest
        src { downloadVersionJson.dest.json.downloads.get(id).url }
        dest file(PATH_BUILD + 'version/' + id + '.jar')
        useETag 'all'
        onlyIfModified true
        quiet true
    }
    downloadVersionJars.dependsOn(versionTask)
}

/**
 * Download version libraries
 */
task downloadVersionLibraries(type: DownloadLibraries) {
    group = 'mcplus download'
    description = 'Downloads libraries needed by game version.'
    dependsOn downloadVersionJson

    json = downloadVersionJson.dest
    config = CONFIG // TODO check what we need
    dest = file(PATH_LIBRARIES)
}

/**
 * Download client assets
 */
task downloadVersionAssets(type: DownloadAssets) {
    group = 'mcplus download'
    description = 'Downloads assets used by game version.'
    dependsOn downloadVersionJson

    json = downloadVersionJson.dest
    dest = file(PATH_BUILD + '/assets/')
}


/**
 * Download official mappings for client and server
 */
task downloadMappings(group: 'mcplus download') {
    description = 'Downloads official mappings.'
}

['client', 'server'].each { side ->
    def taskName = 'download' + side.capitalize() + "Mappings"
    def mappingsTask = task "${taskName}" (type: Download) {
        group = 'mcplus process ' + side
        dependsOn downloadVersionJson

        inputs.file downloadVersionJson.dest
        src { downloadVersionJson.dest.json.downloads.get(side + '_mappings').url }
        dest file(PATH_BUILD + 'mappings/' + side + '_mappings.txt')
        useETag 'all'
        onlyIfModified true
        quiet true
    }
    downloadMappings.dependsOn(mappingsTask)
}

/**
 * Convert official mappings to TSRG2 format
 */
task convertMappings(group: 'mcplus process') {
    description = 'Converts official mappings to TSRG format.'
}

['client', 'server'].each { side ->
    def taskName = 'convert' + side.capitalize() + "Mappings"
    def convertTask = task "${taskName}" (type: ConvertMappings) {
        group = 'mcplus process ' + side
        dependsOn tasks.getByPath("download${side.capitalize()}Mappings")

        input =  tasks.getByPath("download${side.capitalize()}Mappings").dest
        dest = file(PATH_BUILD + 'mappings/' + side + '_mappings.tsrg')
    }
    convertMappings.dependsOn(convertTask)
}


/**
 * Filter version jars to contain only classes
 */
task filterVersionJars(group: 'mcplus process') {
    description = 'Filters version jars.'
}

['client', 'server'].each { side ->
    def taskName = 'filterVersion' + side.capitalize() + "Jar"
    def filterTask = task "${taskName}" (type: SplitJar) {
        group = 'mcplus process ' + side
        dependsOn tasks.getByPath("convert${side.capitalize()}Mappings")
        dependsOn tasks.getByPath("downloadVersion${side.capitalize()}Jar")

        input = tasks.getByPath("downloadVersion${side.capitalize()}Jar").dest
        mappings = tasks.getByPath("convert${side.capitalize()}Mappings").dest
        acceptable = [ "assets/", "data/", "net/minecraft/", "com/mojang/blaze3d", "com/mojang/realms",
                       "log4j2.xml", "pack.mcmeta", "pack.png", "version.json" ]
        sources = file(PATH_BUILD + 'version/' + side + '.sources.jar')
        resources = file(PATH_BUILD + 'version/' + side + '.resources.jar')
        libraries = file(PATH_BUILD + 'version/' + side + '.libraries.jar')
    }
    filterVersionJars.dependsOn(filterTask)
}


/**
 * Merge client and server JAR to one annotated with @OnlyIn(Dist.<SIDE>)
 */
task mergeVersionJars(type: MergeJar) {
    group = 'mcplus process'
    description = 'Merges source JARs into one annotated with Dist marker.'
    dependsOn downloadMergeTool
    dependsOn filterVersionClientJar
    dependsOn filterVersionServerJar

    config MERGETOOL, downloadMergeTool
    client filterVersionClientJar.sources
    server filterVersionServerJar.sources
    dest file(PATH_BUILD + 'version/joined.jar')
    annotations 'API'
}


/**
 * Process sides configuration
 */
def sides = [
    joined: [Name: 'Joined', jsonlibs: true,  extra: 'Client', assets: true,
             mappingsTask: convertClientMappings,
             splitTask: mergeVersionJars,
             resources: [filterVersionClientJar.resources, filterVersionServerJar.resources],
             bundle: null],
    client: [Name: 'Client', jsonlibs: true,  extra: 'Client', assets: true,
             mappingsTask: convertClientMappings,
             splitTask: filterVersionClientJar,
             resources: [filterVersionClientJar.resources],
             bundle: null],
    server: [Name: 'Server', jsonlibs: false, extra: 'Server', assets: false,
             mappingsTask: convertServerMappings,
             splitTask: filterVersionServerJar,
             resources: [filterVersionServerJar.resources],
             bundle: downloadVersionServerJar]
]


/**
 * Prepare list of libraries for Vignette and Fernflower
 */
task generateProcessLibraries(group: 'mcplus process') {
    description = 'Renames jars using mappings'
}

sides.each { s, child ->
    def taskName = 'generateProcess' + child.Name + 'Libraries'
    child.librariesTask = task "${taskName}" (type: CreateFernflowerLibraries) {
        group = 'mcplus process ' + s
        dependsOn downloadVersionLibraries
        dependsOn downloadVersionJson
        dependsOn child.splitTask

        meta = downloadVersionJson.dest
        config = CONFIG
        side = s
        root = file(PATH_LIBRARIES)
        dest file(PATH_BUILD + 'mapped/' + s + '.fernflower.libs.txt')
    }
    generateProcessLibraries.dependsOn(child.librariesTask)
}

/**
 * Vignette renaming
 */
task renameVersionJars(group: 'mcplus process') {
    description = 'Renames jars using mappings'
}

sides.each { side, child ->
    def taskName = 'renameVersion' + child.Name + 'Jar'
    child.renameTask = task "${taskName}" (type: RemapJar) {
        group = 'mcplus process ' + side
        dependsOn downloadRenameTool
        dependsOn child.splitTask
        dependsOn child.mappingsTask
        dependsOn child.librariesTask

        config RENAMETOOL, downloadRenameTool
        input = 'joined'.equals(side) ? child.splitTask.dest : child.splitTask.sources // Use dest if input is from MergeJars
        mappings = child.mappingsTask.dest
        libraries = child.librariesTask.dest
        dictionary = file(PATH_PROJECT + 'dictionary.dict')
        dest = file(PATH_BUILD + 'mapped/' + side + '.mapped.jar')
        log = file(PATH_BUILD + 'mapped/' + side + '.mapped.log')
    }
    renameVersionJars.dependsOn(child.renameTask)
}

/**
 * Fernflower decompilation
 */
task decompileVersionJars (group: 'mcplus process') {
    description = 'Decompiles jars'
}

sides.each { side, child ->
    def taskName = 'decompileVersion' + child.Name + 'Jar'
    child.decompileTask = task "${taskName}" (type: FernflowerTask) {
        group = 'mcplus process ' + side
        dependsOn downloadFernflower
        dependsOn child.librariesTask
        dependsOn child.renameTask

        config FERNFLOWER, downloadFernflower
        javaLauncher = javaToolchains.launcherFor {
            languageVersion = JavaLanguageVersion.of(JAVA_TARGET)
        }
        libraries = child.librariesTask.dest
        input = child.renameTask.dest

        log = file(PATH_BUILD + '/decompile/' + side + '.decompile.log')
        dest = file(PATH_BUILD + '/decompile/' + side + '.decompile.jar')
    }
    decompileVersionJars.dependsOn(child.decompileTask)
}




task createProjectsRoot(type: Copy) {
    group = 'mcplus project'

    from rootProject.file('templates/root_settings.gradle')
    into file(PATH_PROJECTS)
    rename { 'settings.gradle' }
    expand([version: project.name])
}

task extractNatives(type: ExtractNatives) {
    group = 'mcplus project'
    dependsOn downloadVersionJson
    dependsOn downloadVersionLibraries

    meta downloadVersionJson.dest
    cache file(PATH_LIBRARIES)
    dest file(PATH_PROJECTS_NATIVES)
}



/**
 * Apply patches
 */
task projectApplyPatches (group: 'mcplus project') {
    description = 'Applies patches to projects'
}

sides.each { side, child ->
    def project_dir = project.file(PATH_PROJECTS + side)
    def patches_dir = project.file(PATH_SOURCE_PATCHES + side)


    child.projectTask = task "project${child.Name}"(type: CreateProjectTemplate) {
        group = 'mcplus project ' + side

        dependsOn createProjectsRoot
//        dependsOn child.splitTask
        dependsOn child.decompileTask
//        dependsOn child.pkgs
        dependsOn(child.jsonlibs ? [downloadVersionJson, extractNatives] : [])
        dependsOn(child.assets ? [downloadVersionAssets] : [])
//        dependsOn (child.bundle != null ? [child.bundle] : [])

//        mustRunAfter child.delete

        dest project_dir
        distro side
        template rootProject.file('templates/build.gradle')
        if (child.jsonlibs)
            meta downloadVersionJson.dest
        if (child.bundle != null)
            bundle = child.bundle.dest
        CONFIG?.libraries?.get(side)?.each { library "'${it}'" }

        if (child.resources != null)
            child.resources.each { res -> libraryFile res }

        //libraryFile child.splitTask.libraries

        replace '{java_target}', JAVA_TARGET + ''
        //replaceFile '{inject}', PATH_INJECT
        replaceFile '{assets}', child.assets ? downloadVersionAssets.dest : null
        replaceFile '{natives}', child.jsonlibs ? extractNatives.dest : null
        //replaceFile '{merged_src}', MERGE_PATCHES ? projectSharedReset.target : null

        doLast {
            def directory = new File(project_dir, 'src/main/java')
            System.out.println("Creating directory " + directory)
            mkdir directory
        }
    }

    child.resetTask = task "project${child.Name}Reset"(type: ResetSourcesTask) {
        group = 'mcplus project ' + side
//        dependsOn child.resetPkgs
//        dependsOn child.makeSrcDir
        dependsOn child.projectTask
        dependsOn child.decompileTask

//        mustRunAfter child.project

        rootZip child.decompileTask.dest
        target new File(project_dir, 'src/main/java')
    }

    child.makePatchesDir = task "project${child.Name}MakePatchesDir"() {
        group = 'mcplus project ' + side
        doLast {
            mkdir patches_dir
        }
    }

    child.apply = task "project${child.Name}ApplyPatches"(type: ApplyPatchesTask) {
        group = 'mcplus project ' + side
        dependsOn child.resetTask
        dependsOn child.makePatchesDir

        target new File(project_dir, 'src/main/java')
        patches patches_dir
    }
    projectApplyPatches.dependsOn(child.apply)

    child.makePatches = task "project${child.Name}MakePatches"(type: MakePatchesTask) {
        group = 'mcplus project ' + side
        dependsOn child.decompileTask
        dependsOn child.makePatchesDir
        //dependsOn MERGE_PATCHES ? [deduplicateSources, projectSharedMakePatches]

        rootZip child.decompileTask.dest
        target new File(project_dir, 'src/main/java')
        patches patches_dir
    }
}

/**
 * Init repositories
 */
task repositoryInitAll (group: 'mcplus repository') {
    description = 'Inits repositories in all projects'
}

/**
 * Synchronize repos
 */
task repositorySyncAll (group: 'mcplus repository') {
    description = 'Synchronizes distro repositories with JOINED'
}

sides.each { side, child ->
    def project_dir = project.file(PATH_PROJECTS + side)

    child.repoInitTask = task "repository${child.Name}Init"(type: InitRepository) {
        group = 'mcplus repository ' + side
        dependsOn child.projectTask

        target = project_dir
    }
    repositoryInitAll.dependsOn(child.repoInitTask)

    /**
     * Tasks for distro (client, server) only repositories
     */
    if (!side.equals('joined')) {
        child.repoSyncTask = task "repository${child.Name}Sync"(type: SyncChanges) {
            group = 'mcplus repository ' + side
            dependsOn child.repoInitTask
            dependsOn sides.joined.repoInitTask // depend both on JOINED
            mustRunAfter sides.joined.repoInitTask

            target = child.repoInitTask.target
            remoteName = "joined"
            remoteUrl = project_dir.relativePath(sides.joined.repoInitTask.target)
        }
        repositorySyncAll.dependsOn(child.repoSyncTask)
    }
}
